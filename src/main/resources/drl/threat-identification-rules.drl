package MusesDrl


global eu.musesproject.server.eventprocessor.correlator.global.StatusGlobal StatusGlobal
global eu.musesproject.server.eventprocessor.correlator.global.Rt2aeGlobal Rt2aeGlobal

import eu.musesproject.server.eventprocessor.correlator.model.owl.AdditionalProtection
import eu.musesproject.server.eventprocessor.correlator.model.owl.AccessRequest
import eu.musesproject.server.eventprocessor.correlator.model.owl.DeviceProtectionEvent
import eu.musesproject.server.eventprocessor.correlator.model.owl.Event
import eu.musesproject.server.eventprocessor.correlator.model.owl.FileObserverEvent
import eu.musesproject.server.eventprocessor.correlator.model.owl.ConnectivityEvent
import eu.musesproject.server.eventprocessor.correlator.model.owl.Threat;
import eu.musesproject.server.eventprocessor.util.Constants
import java.util.ArrayList
import java.util.Date
import java.util.StringTokenizer

declare Event
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare AccessRequest
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare ConnectivityEvent
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare FileObserverEvent
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare Threat
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare DeviceProtectionEvent
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end

declare AdditionalProtection
	@role( event )
	@timestamp ( event_date )
	@expires (1m)
end


	
rule "Threat-identification-000001"
	no-loop true
	dialect "mvel"
	when
		request: AccessRequest()
		e: ConnectivityEvent(this before[0,5s] request,bluetoothConnected=="TRUE")		
	then
		
		StatusGlobal.log("3.A new threat has been identified, associated to request:"+request.getId());
		StatusGlobal.log(e.getBluetoothConnected());		
		StatusGlobal.log("AccessRequest identifier:"+request.getId());
		Threat threat = Rt2aeGlobal.composeThreat(request,e);
		insert(threat);
	end
	
rule "Threat-identification-000002"
	no-loop true
	dialect "mvel"
	when
		original: ConnectivityEvent()
		e: ConnectivityEvent(this after[0,24h] original, bluetoothConnected != original.bluetoothConnected)		
	then		
		StatusGlobal.log("F1:4 Detection of connection properties changes");
		StatusGlobal.addFlag("F1:4");
		//retract(original);
		//retract(e);	
		//TODO: Detect what is the difference between connectivity properties and if it is a DETERIORATION	
	end
	
rule "Threat-identification-000003"
	no-loop true
	dialect "mvel"
	when
		request: AccessRequest()
		conn: ConnectivityEvent(bssid=="AcmeIntranet")		
	then		
		StatusGlobal.log("F1:7 Detection of user connecting to the company intranet");
		StatusGlobal.addFlag("F1:7");
		StatusGlobal.log("AccessRequest identifier:"+request.getId());
		retract(conn);	
	end	

rule "Threat-identification-000004"
	no-loop true
	dialect "mvel"
	when
		fileEvent: FileObserverEvent(path matches ".*confidential.*")	
	then		
		StatusGlobal.log("F2:8 Detection of sensitive info");
		StatusGlobal.addFlag("F2:8");
		//TODO Call a mechanism to infer the sensitivity level of each asset
		retract(fileEvent);	
	end		

rule "Threat-identification-000005"
	no-loop true
	dialect "mvel"
	when
		request: AccessRequest()
		conn: ConnectivityEvent(hiddenSSID==false,wifiNeighbors>50)		
	then		
		StatusGlobal.log("F1:8 Detection of connection to unsecure Wifi. SSID is not hidden and it has "+conn.getWifiNeighbors()+ " neighbors");
		StatusGlobal.addFlag("F1:8");
		StatusGlobal.log("AccessRequest identifier:"+request.getId());	
	end		

rule "Threat-identification-000006"
	no-loop true
	dialect "mvel"
	when
		request: AccessRequest()
		conn: ConnectivityEvent(hiddenSSID==false,wifiConnected==true,wifiEnabled==true)
	then		
		StatusGlobal.log("F2:1 Detection of unsafe communication settings:"+conn.getId());
		StatusGlobal.addFlag("F2:1");	
		StatusGlobal.log("AccessRequest identifier:"+request.getId());
	end
	
			
rule "Additional Protection Detection"
	no-loop true
	dialect "mvel"
	when
		request: AccessRequest()
		e: DeviceProtectionEvent()
	then
		StatusGlobal.log("An additional protection event has been detected");
		StatusGlobal.log("AccessRequest identifier:"+request.getId());
		AdditionalProtection protection = Rt2aeGlobal.composeAdditionalProtection(request,e);
		insert(protection);
		StatusGlobal.log("Additional Protection inserted in the WM. AccessRequest identifier:"+request.getId());				
	end	
